" dein scripts
if &compatible
  set nocompatible               " Be iMproved
endif

call plug#begin('~/.vim/plugged')
Plug 'airblade/vim-rooter'
Plug 'cespare/vim-toml'
Plug 'fatih/vim-go'
Plug 'junegunn/fzf', { 'build': './install --all' }
Plug 'junegunn/fzf.vim'
Plug 'junegunn/vim-peekaboo'
Plug 'kshenoy/vim-signature'
Plug 'ludovicchabant/vim-gutentags'
Plug 'maralla/completor.vim'
Plug 'mhinz/vim-startify'
Plug 'morhetz/gruvbox'
Plug 'pangloss/vim-javascript'
Plug 'pelodelfuego/vim-swoop'
Plug 'rgarver/Kwbd.vim'
Plug 'romainl/vim-qf'
Plug 'romainl/vim-qlist'
Plug 'sheerun/vim-polyglot'
Plug 'simnalamburt/vim-mundo'
Plug 'tommcdo/vim-lion'
Plug 'tpope/vim-abolish'
Plug 'tpope/vim-characterize'
Plug 'tpope/vim-commentary'
Plug 'tpope/vim-endwise'
Plug 'tpope/vim-fugitive'
Plug 'tpope/vim-repeat'
Plug 'tpope/vim-rsi'
Plug 'tpope/vim-sensible'
Plug 'tpope/vim-surround'
Plug 'tpope/vim-unimpaired'
Plug 'troydm/zoomwintab.vim'
Plug 'tweekmonster/fzf-filemru'
Plug 'vim-pandoc/vim-pandoc'
Plug 'vim-pandoc/vim-pandoc-syntax'
Plug 'w0rp/ale'
Plug 'wellle/targets.vim'
call plug#end()

" Required:
filetype plugin indent on
syntax enable

let g:completor_python_binary = '/usr/local/python/python-2.7/bin/python'
let g:completor_debug = 1

" Clear my custom augroup.
augroup custom
  autocmd!
augroup END

" Preferences
colorscheme gruvbox
set background=dark
let g:gruvbox_sign_column="bg0"

let mapleader = " "
let localleader = '\\'

set hidden               " Allow for hidden buffers
set list                 " show invisibles
set showbreak=↪          " indicate a line is the continuation of a wrapped line
set showmatch            " show matching parenthesis
set showcmd              " show partial commands
set foldlevelstart=99    " Start with all folds open
set dictionary=/usr/share/dict/words        " for spell checking
set spellfile=$HOME/.vim/data/en.utf-8.add  " to add words
set spelllang=en_us      " yes, i do speak it
set spellsuggest=best,10 " only display the 10 best suggestions
set number               " line numbers
set nostartofline        " don't move the cursor to the start of the line when
                         " we move vertically in the buffer
set pumheight=10         " show no more than 10 entries in completion menus
set sidescroll=1         " scroll horizontally by 1
set ignorecase           " ignore case when searching, ...
set smartcase            " unless I search for uppercase
set hlsearch             " and highlight searches
set backupdir=$HOME/.vim/data/vimbackup/ " save backups in alternate location
set directory=$HOME/.vim/data/vimswap/   " save swap files here
set viewdir=$HOME/.vim/data/vimviews/    " save views here
set wildmenu              " commandline completion is nice
set expandtab             " by default, use spaces & no tabs
set shiftwidth=4          " shift width (>> & autoindent)
set softtabstop=4         " 1 soft-tab = 4 spaces
set textwidth=80          " wrap at 80 columns
set nojoinspaces          " 1 space when joining lines at a period, not 2
set notimeout             " don't timeout mappings
set modeline              " allow vim config in files

" Use rg for grep
set grepprg=rg\ -S\ --vimgrep\ --no-heading
set grepformat=%f:%l:%c:%m,%f:%l%m,%f\ \ %l%m|

set splitbelow            " split below instead of above
set splitright            " split to the right instead of the left
set colorcolumn=+1        " highlight the color column+1
set breakindent           " visually indent wrapped lines
set showbreak=⣿⣿⣿⣿⣿
set infercase
set synmaxcol=800
set undofile              " track undo history
set undodir=$HOME/.vim/data/vimundo/ " place undo history here
"set clipboard^=unnamed,unnamedplus " integrate with the system clipboard.
set clipboard=unnamed

set fillchars=         " reset fillchars
set fillchars+=fold:-  " fill foldtext with dashes
set fillchars+=diff:⣿  " indicate deleted lines in diffs with ⣿
set fillchars+=vert:│  " indicate vertical splits with │
set fillchars+=stlnc:━ " indicate horizontal splits with -

set listchars=            " reset listchars
set listchars+=tab:▸\     " display tabs
set listchars+=extends:❯  " hint that theres more to the right, ...
set listchars+=precedes:❮ " hint that theres more to the left, ...

set shortmess=   " reset shortmess
set shortmess+=f " '(3 of 5)' instead of '(file 3 of 5)'
set shortmess+=i " '[noeol] instead of '[Incompelte last line]'
set shortmess+=l " '999L, 888C' instead of '999 lines, 888 characters'
set shortmess+=m " '[+]' instead of '[Modified]'
set shortmess+=n " '[New]' instead of '[New File]'
set shortmess+=r " '[RO]' instead of '[readonly]'
set shortmess+=w " '[w]' instead of 'written' '[a]' instead of 'appended'
set shortmess+=x " '[dos]' instead of '[dos format]', '[unix]' instead of
                 "  '[unix format]', '[mac]' instead of '[mac format]'
set shortmess+=o " overwrite message for writing a file with subsequent message
                 "  for reading a file (useful for ":wn" or when 'autowrite' on)
set shortmess+=O " message for reading a file overwrites any previous message.
                 "  Also for quickfix message (e.g., ":cn").
set shortmess+=t " truncate file message at the start if it is too long to fit
                 "  on the command-line, "<" will appear in the left most column.
                 "  Ignored in Ex mode.
set shortmess+=T " truncate other messages in the middle if they are too long to
                 "  fit on the command line.  "..." will appear  in the middle.
                 "  Ignored in Ex mode.
set shortmess+=I " don't give the intro message when starting Vim |:intro|.
set shortmess+=c " don't give information about completion.

set completeopt=         " reset completeopt
set completeopt+=preview
set completeopt+=menuone  " show the menu when there are matches
set completeopt+=noselect " make me select it.
set completeopt+=noinsert " make me insert it.

set viminfo+=n~/.vim/data/viminfo " move the viminfo file into ~/.vim/data

set virtualedit=        " reset virtualedit
set virtualedit+=block  " can move past the end of the line in visual block mode
set virtualedit+=insert " can move past the end of the line in insert mode

set formatoptions=   " reset formatoptions
set formatoptions+=c " autowrap comments using textwidth
set formatoptions+=q " format comments with gq
set formatoptions+=n " recognize numbered lists
set formatoptions+=j " joining comments deletes comment leader

set wildignore=                                     " reset wildignore
set wildignore+=*/.git/*,*/.hg/*,*/.svn/*,*/CVS/*   " ignore vcs directories
set wildignore+=*.jpg,*.bmp,*.gif,*.png,*.jpeg      " ignore images
set wildignore+=*.o,*.so,*.out                      " ignore compiled objects
set wildignore+=*.sw?                               " ignore swap files
set wildignore+=*.DS_Store                          " ignore mac crap
set wildignore+=*.pyc,*.pyo                         " ignore compiled python
set wildignore+=*.log,*.LOG                         " ignore log files
set wildignore+=*.[12345678]                        " ignore rotated logs
set wildignore+=*.gz,*.tar,*.tgz,*.bz2,*.cpio,*.rpm " ignore archives
set wildignore+=*.ignore                            " ignore extras
set wildignore+=*/__pycache__/*                     " ignore more compiled objects

set diffopt=           " reset diffopt
set diffopt+=filler    " add filler
set diffopt+=vertical  " vertical diff by default

set statusline=         " clear statusline
set statusline+=%F\     " full filename
set statusline+=%m%r%w  " flags
set statusline+=%y\     " filetype
set statusline+=%=      " seperator
set statusline+=c:%c\   " column
set statusline+=l:%l/%L " line

set guioptions-=e  " use textual tab pages
set guioptions-=m  " no menu bar
set guioptions-=r  " no scroll bar (right)
set guioptions-=R  " no scroll bar (right) for splits
set guioptions-=l  " scroll bar (left)
set guioptions-=L  " scroll bar (left) for splits
set guioptions-=T  " toolbar

" [S]plit lines (sister to [J]oin line)
function! s:SplitLine()
  exe "normal! i\<CR>\<Esc>^gk"
  silent! substitute/\v +$//
  silent! nohlsearch
  call histdel("search", -1)
  normal! $
endfunction
nnoremap S :call <SID>SplitLine()<CR>

function! Preserve(command)
  " Preparation: save last search, and cursor position.
  let _s=@/
  let l = line(".")
  let c = col(".")
  " Do the business:
  execute a:command
  " Clean up: restore previous search history, and cursor position
  let @/=_s
  call cursor(l, c)
endfunction

function! RemoveTrailingWhiteSpace()
    " Allow a buffer local variable trailing_witespace_ok  to permit trailing
    " whitspace -- sometimes I need to do this if im editing someone elses code
    " and don't want to own the entire file in vcs. Enable it with let
    " trailing_witespace_ok=1, disable it with unlet trailing_witespace_ok
    if !exists("b:trailing_whitespace_ok")
        call Preserve("%s/\\s\\+$//e")
    endif
endfun

function! RestoreCursorPosition()
    " Restore cursor position to the last time you were in the file, this uses
    " marks so it's dependent on viminfo
    for ft in ['gitcommit', 'hgcommit', 'cvs', 'svn']
        if &filetype==ft
            return
        endif
    endfor
    normal! g`"
    normal! zz
endfunction

" Try really hard to use the correct syntax highlighting
autocmd custom BufEnter * :syntax sync fromstart

" Tweak basic navigation
nnoremap j gj
nnoremap k gk
nnoremap $ g$

nnoremap gj j
nnoremap gk k
nnoremap g$ $

onoremap j gj
onoremap k gk
onoremap $ g$

onoremap gj j
onoremap gk k
onoremap g$ $

vnoremap j gj
vnoremap k gk
vnoremap $ g$

vnoremap gj gj
vnoremap gk gk
vnoremap g$ g$

" smart-home: ^ and 0 will toggle between one another {{{
nnoremap <expr> ^ col('.') == match(getline('.'),'\S')+1 ? 'g0' : 'g^'
vnoremap <expr> ^ col('.') == match(getline('.'),'\S')+1 ? 'g0' : 'g^'
onoremap <expr> ^ col('.') == match(getline('.'),'\S')+1 ? 'g0' : 'g^'
nnoremap <expr> 0 col('.') == 1 ? 'g^' : 'g0'
vnoremap <expr> 0 col('.') == 1 ? 'g^' : 'g0'
onoremap <expr> 0 col('.') == 1 ? 'g^' : 'g0'

" When using marks, all by default mark columns as well
nnoremap ' `

" Simple map to disable highlighting
nnoremap // :nohl<cr>

" Reselect visually selected text after indenting
vnoremap > >gv
vnoremap < <gv

" sudo write-file in case you're in a file you dont have privileges to write{{{
cmap w!! w !sudo tee % >/dev/null<cr>

" . returns to starting place after repeat
nnoremap . .`[

" Don't move when you use */#
" https://twitter.com/dotvimrc/status/428208518487764992
nnoremap <silent> * :let b:pos = winsaveview()<cr>*:call winrestview(b:pos)<cr>
nnoremap <silent> # :let b:pos = winsaveview()<cr>#:call winrestview(b:pos)<cr>

" File commands
nnoremap <leader>fed :e $HOME/.dotfiles/vim/vimrc<CR>
nnoremap <leader>ff :FilesMru --tiebreak=end<CR>
nnoremap <leader>fm :History<CR>
nnoremap <leader>fh :History<CR>

" Project commands
nnoremap <leader>pp :Projects<CR>
nnoremap <leader>pf :GFiles<CR>
nnoremap <leader>pg :GFiles<CR>

" Buffer commands
nnoremap <leader>bb :Buffers<CR>
nnoremap <leader>bd :Kwbd<CR>
nnoremap <leader>bc :Kwbd<CR>
nnoremap <leader>bn :bnext<CR>
nnoremap <leader>bp :bprev<CR>
nnoremap <leader>bq :copen<CR>
nnoremap <leader>bl :lopen<CR>
nnoremap <leader>b<tab> <C-^>
nnoremap <C-C><Left> <C-^>
nnoremap <leader>bs :Scratch<CR>

command! -bang Notes call fzf#run(fzf#wrap
      \  ('my-stuff', {'dir': '~/notes',
      \                'source': 'rg --files -g "*.md" -g "!archive/" -g "!contrib/"'
      \               },
      \  <bang>0)
      \ )

function! NoteToday()
  let s:basedir = expand('~/notes/daily')
  let s:dir = expand('~/notes/daily') . strftime("/%Y/%m/")
  let s:file = s:dir . strftime("%Y%m%d.md")
  silent execute system('mkdir -p ' . s:dir)
  execute "edit " . s:file
endfunction

nnoremap <leader>an :Notes<cr>
nnoremap <leader>aN :call NoteToday()<cr>
nnoremap <leader>at :terminal ++curwin<cr>

" Other commands
nnoremap <leader>as :call Swoop()<cr>
vnoremap <leader>as :call SwoopSelection()<cr>
nnoremap <leader>aS :call Swoop()<cr>
vnoremap <leader>aS :call SwoopMultiSelection()<cr>
" Replace word under cursor, everywhere in the file.
nnoremap <leader>r. :%s/\<C-r><C-w>\>/
" Replace visually selected characters, everywhere in the file.
vnoremap <leader>rv "hy:%s/<C-r>h/
nnoremap <leader>au :MundoToggle<cr>

" Tag commands
nnoremap <leader>Tt :Tags<cr>
" go to definition / tag
nnoremap <leader>Td <C-]>
" go to tag in a split
nnoremap <leader>Twd <C-w>}
nnoremap <leader>Tn :tnext<cr>
nnoremap <leader>Tp :tprevious<cr>

" Tab commands
nnoremap <leader>tt :tabs<cr>
nnoremap <leader>td :tabclose<cr>
nnoremap <leader>tp :tabprevious<cr>
nnoremap <leader>tn :tabnext<cr>
nnoremap <leader>th :tabprevious<cr>
nnoremap <leader>tl :tabnext<cr>
nnoremap <leader>tj :tabprevious<cr>
nnoremap <leader>tk :tabnext<cr>
nnoremap <leader>tH :tabfirst<cr>
nnoremap <leader>tL :tablast<cr>
nnoremap <leader>tJ :tabfirst<cr>
nnoremap <leader>tK :tablast<cr>
nnoremap <leader>tN :tabnew<cr>
nnoremap <leader>to :tabonly<cr>

" Window commands
nnoremap <leader>w= <C-W>=
nnoremap <leader>w> <C-W>>
nnoremap <leader>w< <C-W><
nnoremap <leader>w<right> <C-W>>
nnoremap <leader>w<left> <C-W><
nnoremap <leader>w<down> <C-W>-
nnoremap <leader>w<up> <C-W>+
nnoremap <C-W>- <C-W>s
nnoremap <C-W>/ <C-W>v
tnoremap <C-W>- <C-W>s
tnoremap <C-W>/ <C-W>v
tnoremap <C-W>o <C-W>N:ZoomWinTabToggle<cr>a
tnoremap <C-W>z <C-W>N:ZoomWinTabToggle<cr>a
nnoremap <leader>w- <C-W>s
nnoremap <leader>w/ <C-W>v
nnoremap <leader>wj <C-W>j
nnoremap <leader>wh <C-W>h
nnoremap <leader>wk <C-W>k
nnoremap <leader>wl <C-W>l
nnoremap <leader>wd <C-W>c
nnoremap <leader>wc <C-W>c
nnoremap <leader>wL <C-W>r
nnoremap <leader>wH <C-W>R
nnoremap <leader>ww :Windows<cr>
nnoremap <leader>w<tab> <C-W>p
nnoremap <leader>wo :ZoomWinTabToggle<cr>
nnoremap <leader>wz :ZoomWinTabToggle<cr>

" Git commands
nnoremap <leader>gs :Gstatus<cr>
nnoremap <leader>gc :Gcommit -v<cr>
nnoremap <leader>gl :Glog<cr>
nnoremap <leader>gd :Gdiff<cr>
nnoremap <leader>gb :Gblame<cr>
nnoremap <leader>gf :GFiles<CR>

" grep for word under cursor
nmap <Leader># #:sil! gr! "\b<C-R><C-W>\b"<CR>:cw<CR>:redr!<CR>
nmap <Leader>* #:sil! gr! "\b<C-R><C-W>\b"<CR>:cw<CR>:redr!<CR>

let g:ale_sign_column_always = 1
let g:ale_fixers = {
      \  'python': [
      \    'yapf',
      \    'isort',
      \    'add_blank_lines_for_python_control_statements',
      \    'remove_trailing_lines',
      \    'trim_whitespace',
      \  ],
      \ }
let g:ale_linters = {
      \  'python': [
      \    'flake8',
      \    'pylint',
      \  ],
      \ }
let g:ale_set_quickfix = 1
let g:ale_set_loclist = 0
let g:ale_lint_on_insert_leave = 1
let g:ale_lint_on_text_changed = "normal"
let g:ale_lint_delay = 0


autocmd custom BufWinEnter * if &buftype == 'terminal' | setlocal colorcolumn=0 | setlocal nonumber | endif

"nnoremap ≈ :Commands<cr>
nnoremap <M-x> :Commands<cr>
nnoremap <leader>hh :help ShortcutContents<cr>
nnoremap <leader>ht :Helptags<cr>
nnoremap <leader>h. :help <C-r><C-w><cr>

nnoremap <leader>qq :qall<cr>
nnoremap <leader>qr :RestartVim<cr>

command! -bang -nargs=* Rg
      \ call fzf#vim#grep(
      \ 'rg --column --line-number --no-heading --smart-case --color=always '.shellescape(<q-args>),
      \ 1,
      \ <bang>0 ? fzf#vim#with_preview('up:60%')
      \         : fzf#vim#with_preview('right:50%:hidden', '?'),
      \ <bang>0)

" quickfix customizations
autocmd custom FileType qf nnoremap <buffer> q :q<cr>
autocmd custom FileType qf set nobuflisted
autocmd custom FileType qf setlocal nolist
autocmd custom FileType qf setlocal norelativenumber
autocmd custom FileType qf setlocal nowrap
autocmd custom FileType qf setlocal number

" locationlist customizations
autocmd custom FileType ll nnoremap <buffer> q :q<cr>
autocmd custom FileType ll set nobuflisted
autocmd custom FileType ll setlocal nolist
autocmd custom FileType ll setlocal norelativenumber
autocmd custom FileType ll setlocal nowrap
autocmd custom FileType ll setlocal number

" make customizations
autocmd custom FileType make autocmd BufWritePre <buffer> call RemoveTrailingWhiteSpace()
autocmd custom FileType make setlocal noexpandtab
autocmd custom FileType make setlocal nosmarttab
autocmd custom FileType make setlocal softtabstop=0
autocmd custom FileType make setlocal tabstop=4

" go customizations
" autocmd custom FileType go autocmd BufWritePre <buffer> call RemoveTrailingWhiteSpace()
autocmd custom FileType go setlocal noexpandtab
autocmd custom FileType go setlocal shiftwidth=4
autocmd custom FileType go setlocal tabstop=4

autocmd custom FileType go nnoremap <buffer> <leader>mdS :call FixVimGoDebugHighlights()<cr>:GoDebugStart<cr>
autocmd custom FileType go nnoremap <buffer> <leader>mdb :call FixVimGoDebugHighlights()<cr>:GoDebugBreakpoint<cr>
autocmd custom FileType go nnoremap <buffer> <leader>mdc :call FixVimGoDebugHighlights()<cr>:GoDebugContinue<cr>
autocmd custom FileType go nnoremap <buffer> <leader>mdn :call FixVimGoDebugHighlights()<cr>:GoDebugNext<cr>
autocmd custom FileType go nnoremap <buffer> <leader>mdr :call FixVimGoDebugHighlights()<cr>:GoDebugRestart<cr>
autocmd custom FileType go nnoremap <buffer> <leader>mds :call FixVimGoDebugHighlights()<cr>:GoDebugStep<cr>
autocmd custom FileType go nnoremap <buffer> <leader>mdx :call FixVimGoDebugHighlights()<cr>:GoDebugStop<cr>
autocmd custom FileType go nnoremap <buffer> <leader>mdX :call FixVimGoDebugHighlights()<cr>:GoDebugStop<cr>
autocmd custom FileType go nnoremap <buffer> <leader>mdo :call FixVimGoDebugHighlights()<cr>:GoDebugStepOut<cr>
" autocmd custom FileType go hi! GoDebugCurrent term=underline ctermbg=237 guibg=#3c3836

function! FixVimGoDebugHighlights() abort
  highlight GoDebugCurrent term=underline ctermbg=237 guibg=#3c3836
endfunction
autocmd custom ColorScheme * call FixVimGoDebugHighlights()


" TODO: change the highlight line for debug in this colorscheme, it makes it
" impossible to read. Currently it's:
" GoDebugCurrent xxx term=reverse ctermfg=7 ctermbg=12 guifg=White guibg=DarkBlue
" Soemthing like this would be better ...
" CursorLine     xxx term=underline ctermbg=237 guibg=#3c3836
let g:go_fmt_command = "goimports"

" yaml customizations
autocmd custom FileType yaml autocmd BufWritePre <buffer> call RemoveTrailingWhiteSpace()
autocmd custom FileType yaml setlocal shiftwidth=4

" man page customizations
autocmd custom FileType man nnoremap <buffer> q :q<cr>
autocmd custom FileType man setlocal nolist
autocmd custom FileType man setlocal nomodifiable
autocmd custom FileType man setlocal nomodified
autocmd custom FileType man setlocal norelativenumber
autocmd custom FileType man setlocal number

" pandoc/markdown customizations
autocmd custom FileType pandoc autocmd BufWritePre <buffer> call RemoveTrailingWhiteSpace()
autocmd custom FileType pandoc iabbrev <expr> <buffer> :date: '<'.strftime("%Y-%m-%d %a").'>'
autocmd custom FileType pandoc iabbrev <expr> <buffer> :time: '['.strftime("%Y-%m-%d %a %H:%M").']'
autocmd custom FileType pandoc iabbrev <expr> <buffer> :closed: 'CLOSED: ['.strftime("%Y-%m-%d %a %H:%M").']'
autocmd custom FileType pandoc setlocal complete+=kspell
autocmd custom FileType pandoc setlocal shiftwidth=4
autocmd custom FileType pandoc setlocal softtabstop=4
autocmd custom FileType pandoc setlocal spell
autocmd custom FileType pandoc setlocal foldlevel=2
autocmd custom FileType pandoc setlocal foldcolumn=0
" toggling
autocmd custom FileType pandoc nnoremap <buffer> <leader>mtt :call ToggleState("TODO")<cr>
autocmd custom FileType pandoc nnoremap <buffer> <leader>mtd :call ToggleState("DONE")<cr>
autocmd custom FileType pandoc nnoremap <buffer> <leader>mtw :call ToggleState("WAITING")<cr>
autocmd custom FileType pandoc nnoremap <buffer> <leader>mtc :call ToggleState("CANCELED")<cr>
" publishing
autocmd custom FileType pandoc nnoremap <buffer> <leader>meh :call MarkdownPublish("html")<cr>
autocmd custom FileType pandoc nnoremap <buffer> <leader>meo :call MarkdownPublish("org")<cr>
autocmd custom FileType pandoc nnoremap <buffer> <leader>mew :call MarkdownPublish("docx")<cr>
autocmd custom FileType pandoc nnoremap <buffer> <leader>mer :call MarkdownPublish("rst")<cr>
autocmd custom FileType pandoc nnoremap <buffer> <leader>mea :call MarkdownPublish("asciidoc")<cr>
" styles
autocmd custom FileType pandoc nmap <buffer> <leader>msi <Plug>(pandoc-keyboard-toggle-emphasis)
autocmd custom FileType pandoc vmap <buffer> <leader>msi <Plug>(pandoc-keyboard-toggle-emphasis)
autocmd custom FileType pandoc nmap <buffer> <leader>msb <Plug>(pandoc-keyboard-toggle-strong)
autocmd custom FileType pandoc vmap <buffer> <leader>msb <Plug>(pandoc-keyboard-toggle-strong)
autocmd custom FileType pandoc nmap <buffer> <leader>ms` <Plug>(pandoc-keyboard-toggle-verbatim)
autocmd custom FileType pandoc vmap <buffer> <leader>ms` <Plug>(pandoc-keyboard-toggle-verbatim)
autocmd custom FileType pandoc nmap <buffer> <leader>ms~ <Plug>(pandoc-keyboard-toggle-strikeout)
autocmd custom FileType pandoc vmap <buffer> <leader>ms~ <Plug>(pandoc-keyboard-toggle-strikeout)
autocmd custom FileType pandoc nmap <buffer> <leader>ms^ <Plug>(pandoc-keyboard-toggle-superscript)
autocmd custom FileType pandoc vmap <buffer> <leader>ms^ <Plug>(pandoc-keyboard-toggle-superscript)
autocmd custom FileType pandoc nmap <buffer> <leader>ms_ <Plug>(pandoc-keyboard-toggle-subscript)
autocmd custom FileType pandoc vmap <buffer> <leader>ms_ <Plug>(pandoc-keyboard-toggle-subscript)
autocmd custom FileType pandoc vmap <buffer> <silent> aPe <Plug>(pandoc-keyboard-select-emphasis-inclusive)
autocmd custom FileType pandoc vmap <buffer> <silent> iPe <Plug>(pandoc-keyboard-select-emphasis-exclusive)
autocmd custom FileType pandoc omap <buffer> aPe :normal vaPe<cr>
autocmd custom FileType pandoc omap <buffer> iPe :normal viPe<cr>
autocmd custom FileType pandoc vmap <buffer> <silent> aPs <Plug>(pandoc-keyboard-select-strong-inclusive)
autocmd custom FileType pandoc vmap <buffer> <silent> iPs <Plug>(pandoc-keyboard-select-strong-exclusive)
autocmd custom FileType pandoc omap <buffer> aPs :normal vaPs<cr>
autocmd custom FileType pandoc omap <buffer> iPs :normal viPs<cr>
autocmd custom FileType pandoc vmap <buffer> <silent> aPv <Plug>(pandoc-keyboard-select-verbatim-inclusive)
autocmd custom FileType pandoc vmap <buffer> <silent> iPv <Plug>(pandoc-keyboard-select-verbatim-exclusive)
autocmd custom FileType pandoc omap <buffer> aPv :normal vaPv<cr>
autocmd custom FileType pandoc omap <buffer> iPv :normal viPv<cr>
autocmd custom FileType pandoc vmap <buffer> <silent> aPk <Plug>(pandoc-keyboard-select-strikeout-inclusive)
autocmd custom FileType pandoc vmap <buffer> <silent> iPk <Plug>(pandoc-keyboard-select-strikeout-exclusive)
autocmd custom FileType pandoc omap <buffer> aPk :normal vaPk<cr>
autocmd custom FileType pandoc omap <buffer> iPk :normal viPk<cr>
autocmd custom FileType pandoc vmap <buffer> <silent> aPu <Plug>(pandoc-keyboard-select-superscript-inclusive)
autocmd custom FileType pandoc vmap <buffer> <silent> iPu <Plug>(pandoc-keyboard-select-superscript-exclusive)
autocmd custom FileType pandoc omap <buffer> aPu :normal vaPu<cr>
autocmd custom FileType pandoc omap <buffer> iPu :normal viPu<cr>
autocmd custom FileType pandoc vmap <buffer> <silent> aPt <Plug>(pandoc-keyboard-select-subscript-inclusive)
autocmd custom FileType pandoc vmap <buffer> <silent> iPt <Plug>(pandoc-keyboard-select-subscript-exclusive)
autocmd custom FileType pandoc omap <buffer> aPt :normal vaPl<cr>
autocmd custom FileType pandoc omap <buffer> iPt :normal viPl<cr>
" sections
autocmd custom FileType pandoc nmap <buffer> <leader>m# <Plug>(pandoc-keyboard-apply-header)
autocmd custom FileType pandoc nmap <buffer> <leader>mhd <Plug>(pandoc-keyboard-remove-header)
autocmd custom FileType pandoc nmap <buffer> <leader>mhn <Plug>(pandoc-keyboard-next-header)
autocmd custom FileType pandoc nmap <buffer> <leader>mhb <Plug>(pandoc-keyboard-prev-header)
autocmd custom FileType pandoc nmap <buffer> <leader>mhh <Plug>(pandoc-keyboard-cur-header)
autocmd custom FileType pandoc nmap <buffer> <leader>mhp <Plug>(pandoc-keyboard-cur-header-parent)
autocmd custom FileType pandoc nmap <buffer> <leader>mhsn <Plug>(pandoc-keyboard-next-header-sibling)
autocmd custom FileType pandoc nmap <buffer> <leader>mhsb <Plug>(pandoc-keyboard-prev-header-sibling)
autocmd custom FileType pandoc nmap <buffer> <leader>mhcf <Plug>(pandoc-keyboard-first-header-child)
autocmd custom FileType pandoc nmap <buffer> <leader>mhcl <Plug>(pandoc-keyboard-last-header-child)
autocmd custom FileType pandoc nmap <buffer> <leader>mhcn <Plug>(pandoc-keyboard-nth-header-child)
autocmd custom FileType pandoc nmap <buffer> ]] <Plug>(pandoc-keyboard-ff-header)
autocmd custom FileType pandoc nmap <buffer> [[ <Plug>(pandoc-keyboard-rw-header)
autocmd custom FileType pandoc nmap <buffer> ][ <Plug>(pandoc-keyboard-ff-sect-end)
autocmd custom FileType pandoc nmap <buffer> [] <Plug>(pandoc-keyboard-rw-sect-end)
autocmd custom FileType pandoc vmap <buffer> aS <Plug>(pandoc-keyboard-select-section-inclusive)
autocmd custom FileType pandoc omap <buffer> aS :normal VaS<cr>
autocmd custom FileType pandoc vmap <buffer> iS <Plug>(pandoc-keyboard-select-section-exclusive)
autocmd custom FileType pandoc omap <buffer> iS :normal ViS<cr>
" links
autocmd custom FileType pandoc nmap <buffer> <leader>mgl <Plug>(pandoc-keyboard-links-open)
autocmd custom FileType pandoc nmap <buffer> <leader>msl <Plug>(pandoc-keyboard-links-split)
autocmd custom FileType pandoc nmap <buffer> <leader>mgb <Plug>(pandoc-keyboard-links-back)
autocmd custom FileType pandoc nmap <buffer> <leader>mgB <Plug>(pandoc-keyboard-links-file-back)
" lists
autocmd custom FileType pandoc nmap <buffer> <leader>mln <Plug>(pandoc-keyboard-next-li)
autocmd custom FileType pandoc nmap <buffer> <leader>mlp <Plug>(pandoc-keyboard-prev-li)
autocmd custom FileType pandoc nmap <buffer> <leader>mll <Plug>(pandoc-keyboard-cur-li)
autocmd custom FileType pandoc nmap <buffer> <leader>mllp <Plug>(pandoc-keyboard-cur-li-parent)
autocmd custom FileType pandoc nmap <buffer> <leader>mlsn <Plug>(pandoc-keyboard-next-li-sibling)
autocmd custom FileType pandoc nmap <buffer> <leader>mlsp <Plug>(pandoc-keyboard-prev-li-sibling)
autocmd custom FileType pandoc nmap <buffer> <leader>mlcf <Plug>(pandoc-keyboard-first-li-child)
autocmd custom FileType pandoc nmap <buffer> <leader>mlcl <Plug>(pandoc-keyboard-last-li-child)
autocmd custom FileType pandoc nmap <buffer> <leader>mlcn <Plug>(pandoc-keyboard-nth-li-child)
" references
autocmd custom FileType pandoc nmap <buffer> <leader>mnr <Plug>(pandoc-keyboard-ref-insert)
autocmd custom FileType pandoc nmap <buffer> <leader>mrg <Plug>(pandoc-keyboard-ref-goto)
autocmd custom FileType pandoc nmap <buffer> <leader>mrb <Plug>(pandoc-keyboard-ref-backfrom)

let g:pandoc#keyboard#use_default_mappings = 0


" perl customizations
autocmd custom FileType perl autocmd BufWritePre <buffer> call RemoveTrailingWhiteSpace()
autocmd custom FileType perl setlocal iskeyword+=$,%,@

" puppet customizations
autocmd custom FileType puppet autocmd BufWritePre <buffer> call RemoveTrailingWhiteSpace()

" python customizations
autocmd custom FileType python autocmd BufWritePre <buffer> call RemoveTrailingWhiteSpace()
autocmd custom FileType python setlocal textwidth=79
autocmd custom BufNewFile *.py 0r ~/.vim/templates/skeleton.py
"
" javascript customizations
autocmd custom FileType javascript autocmd BufWritePre <buffer> call RemoveTrailingWhiteSpace()
autocmd custom FileType javascript setlocal textwidth=120

" cython customizations
autocmd custom FileType pyrex autocmd BufWritePre <buffer> call RemoveTrailingWhiteSpace()
autocmd custom FileType pyrex setlocal textwidth=79

" ruby customizations
autocmd custom FileType ruby autocmd BufWritePre <buffer> call RemoveTrailingWhiteSpace()

" shell script customizations
autocmd custom FileType sh,zsh autocmd BufWritePre <buffer> call RemoveTrailingWhiteSpace()

" shell vimscript customizations
autocmd custom FileType vim autocmd BufWritePre <buffer> call RemoveTrailingWhiteSpace()
autocmd custom FileType vim setlocal shiftwidth=2
autocmd custom FileType vim setlocal softtabstop=2

" shell vim help page customizations
autocmd custom FileType help nnoremap <buffer> q :q<cr>
autocmd custom FileType help setlocal nolist
autocmd custom FileType help setlocal nomodifiable
autocmd custom FileType help setlocal nomodified
autocmd custom FileType help setlocal norelativenumber
autocmd custom FileType help setlocal number

" swoop buffer customizations
autocmd custom BufEnter swoopBuf nnoremap <buffer> q :bdelete!<cr>

" gitcommit customizations
autocmd custom FileType gitcommit setlocal complete+=kspell
autocmd custom FileType gitcommit setlocal spell

" netrw customizations
autocmd custom FileType netrw nnoremap <buffer> q :q<cr>
autocmd custom FileType netrw setlocal nolist
autocmd custom FileType netrw setlocal nomodifiable
autocmd custom FileType netrw setlocal nomodified
autocmd custom FileType netrw setlocal norelativenumber
autocmd custom FileType netrw setlocal number

" Always restore cursor position to the last place it was when a file was last
" opened.
autocmd custom BufReadPost * call RestoreCursorPosition()

" Automatically bring up location / quickfix lists
autocmd custom QuickFixCmdPost [^l]* cwindow
autocmd custom QuickFixCmdPost    l* lwindow
autocmd custom VimEnter            * cwindow

silent execute '!mkdir -p $HOME/.vim/data/vimbackup'
silent execute '!mkdir -p $HOME/.vim/data/vimswap'
silent execute '!mkdir -p $HOME/.vim/data/vimviews'
silent execute '!mkdir -p $HOME/.vim/data/vimundo'
silent execute '!mkdir -p $HOME/.vim/data/tags'

let g:swoopUseDefaultKeyMap = 0

let g:mundo_help = 1
let g:mundo_preview_bottom = 1
let g:mundo_right = 1

let g:fzf_filemru_bufwrite = 1
let g:fzf_filemru_git_ls = 1

let g:rooter_silent_chdir = 1
let g:rooter_use_lcd = 1

let g:python_highlight_all = 1

let g:markdown_fenced_languages = ['html', 'vim', 'python', 'bash=sh', 'sql', 'erlang']

function! ToggleState(state)
  let l:save = winsaveview()
  exe ":.,.s/<#S \\(TODO\\|WAITING\\|DONE\\|CANCELED\\)/<#S " . a:state . "/"
  call winrestview(l:save)
endfunction

function! MarkdownPublish(format)
  let s:name = expand('%')
  if a:format == "html"
    let s:cmd = "gpp --nostdinc --nocurinc -H " . s:name . " | "
          \ . "pandoc --standalone --toc --toc-depth=2 "
          \ . "--include-in-header=$HOME/.vim/contrib/pandoc/header.html "
          \ . "--include-before-body=$HOME/.vim/contrib/pandoc/before-body.html "
          \ . "--include-after-body=$HOME/.vim/contrib/pandoc/after-body.html "
          \ . "--to=html "
          \ . "--from=markdown+emoji+simple_tables+pipe_tables+grid_tables+multiline_tables+table_captions "
          \ . "--output=" . s:name . ".html"
    silent execute system(s:cmd)
  else
    let s:cmd = "gpp --nostdinc --nocurinc -H " . s:name . " | "
          \ . "pandoc --standalone --toc --toc-depth=2 "
          \ . "--to=" . a:format . " "
          \ . "--from=markdown+emoji "
          \ . "--output=" . s:name . "." . a:format
    silent execute system(s:cmd)
  endif
endfunction

let g:startify_bookmarks = [
      \ '$HOME/.dotfiles/vim/vimrc',
      \ '$HOME/.vimrc.local',
      \ '$HOME/.dotfiles/zshrc',
      \ '$HOME/.zshrc.local',
      \ '$HOME/.dotfiles/zshenv',
      \ '$HOME/.zshenv.local',
      \ '$HOME/.aliases',
      \ '$HOME/.vim/doc/shortcuts.txt',
      \]
"
" Experimentation
set concealcursor=iv
set conceallevel=2
set encoding=utf-8
set fileencoding=utf-8
set lazyredraw
set matchpairs+=<:>
set showfulltag
set termencoding=utf-8

nnoremap <leader>ah :echo "hi<" . synIDattr(synID(line("."),col("."),1),"name") . '> trans<'
      \ . synIDattr(synID(line("."),col("."),0),"name") . "> lo<"
      \ . synIDattr(synIDtrans(synID(line("."),col("."),1)),"name") . ">"<CR>

" refresh all the things.
nnoremap <silent> <c-l> :nohlsearch<cr>:diffupdate<cr>:syntax sync fromstart<cr><c-l>

set belloff+=ctrlg
set belloff+=all

" Insert mode Completion:
" <C-X><C-F> filenames
" <C-X><C-I> identifiers
" <C-X><C-K> words in dictionary
" <C-X><C-L> whole lines
" <C-X><C-O> omni completion function
" <C-X><C-S> spelling suggestions
" <C-X><C-T> thesaurus
" <C-X><C-U> user completion function
" <C-X><C-V> complete like in : command line
" <C-X><C-]> complete tags

command! Scratch enew | setlocal nobuflisted buftype=nofile bufhidden=wipe noswapfile

" Abolish maps are in ~/.vim/after/plugin/abolish.vim
source ~/.vimrc.local
