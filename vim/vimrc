
" Folding cheet sheet
" zR    open all folds
" zM    close all folds
" za    toggle fold at cursor position
" zj    move down to start of next fold
" zk    move up to end of previous fold

" Pre-local loading (set variables to influence module loading) {{{1
" Include user's local vim config
if filereadable(expand("~/.vimrc.pre-local"))
    source ~/.vimrc.pre-local
endif

" Load Plugins. {{{1
call pathogen#runtime_append_all_bundles()
call pathogen#helptags()

" Setup directories {{{1
silent execute '!mkdir -p $HOME/.vimbackup'
silent execute '!mkdir -p $HOME/.vimswap'
silent execute '!mkdir -p $HOME/.vimviews'

" Preferences {{{1
" Use Vim settings, rather then Vi settings (much better!).
" This must be first, because it changes other options as a side effect.
filetype plugin on
filetype plugin indent on
set showfulltag
set cmdheight=2
set nocompatible
set backspace=indent,eol,start
set history=10000       " keep lots of command line history
set undolevels=10000    " lots of undoing too
set showcmd             " display incomplete commands
set incsearch           " do incremental searching
set visualbell t_vb=    " no beep or bell
set number              " line numbers
set cursorline          " highlight the line with the cursor
set cursorcolumn        " highlight the column with the cursor
set tabstop=4           " Tab counts as 4 spaces
set softtabstop=4       " Soft tabs ARE 4 spaces
set shiftwidth=4        " Autoindent using 4 spaces
set textwidth=100
set expandtab           " Expand tabs to spaces
set hidden              " buffers not in the foreground are hidden
set nowrap              " No wrapping
set backupdir=$HOME/.vimbackup/ " backup files
set directory=$HOME/.vimswap/   " swap files
set viewdir=$HOME/.vimviews/    " view files
set laststatus=2        " always show the status line
set clipboard+=unnamed  " use the x11 clipboard for yanking
set scrolloff=5         " keep 5 lines for context while scrolling
set sidescrolloff=5     " same for left-right
set splitbelow          " drr
set ignorecase          " case insensitive searching, ...
set smartcase           " unless the search term includes a capital
set title               " vim changes xterm window title
set foldcolumn=2        " add 2 to the gutter to show folds
set shortmess+=filmnrxoOtTI
set iskeyword+=:
set iskeyword+=_
set iskeyword+=$
set iskeyword+=%
set iskeyword+=@
set iskeyword+=,
set listchars=precedes:<,extends:>,tab:\|_
set virtualedit=block
set display+=lastline
set nolist
set wildmenu
set nowarn
set diffopt=filler,iwhite
set autoread
"set complete=.,w,b,u,U,t,i,d
set ttyfast
set fileformats=unix,mac,dos
set formatoptions+=n2 " look up formatlistpat
set updatecount=10
set wildmode=list:longest,full
set nostartofline
syntax enable
set background=dark
set shiftround
set magic
set gdefault
set matchpairs+=<:>
color ir_black
if &t_Co < 16
    set t_Co=16
endif

set guicursor=n-v-c:block-Cursor
set guicursor+=i:ver20-Cursor-blinkon600-blinkoff600
set guicursor+=n-v-c:block-Cursor-blinkon600-blinkoff600

" In many terminal emulators the mouse works just fine, thus enable it.
if has('mouse')
    set mouse=a
endif

" Switch syntax highlighting on, when the terminal has colors
" Also switch on highlighting the last used search pattern.
syntax on
set hlsearch
"
" Statusline {{{1
set statusline=   " clear the statusline for when vimrc is reloaded
set statusline+=%#KomiteeStatus#
set statusline+=[%n]\                      " buffer number
set statusline+=%<%.99f\                     " file name
set statusline+=%h%m%r%w                     " flags
set statusline+=[%{strlen(&ft)?&ft:'none'},  " filetype
set statusline+=%{strlen(&fenc)?&fenc:&enc}, " encoding
set statusline+=%{&fileformat}               " file format
set statusline+=%{HasPaste()}]               " whether we're in paste mode
set statusline+=%{fugitive#statusline()}     " git branch
set statusline+=%=                           " right align
set statusline+=col:%v\                      " current column
set statusline+=lin:%l/%L\                   " line
set statusline+=%p%%                         " percentage through file

function! HasPaste()
    if &paste
        return ',paste'
    else
        return ''
    endif
endfunction

" Configure ColorColumn
hi ColorColumn guibg=#191919 gui=BOLD ctermbg=NONE cterm=BOLD

" If we change focus away from vim, goto normal mode
au FocusLost,TabLeave * call feedkeys("\<C-\>\<C-n>")

augroup vimmemory
    au!
    " When editing a file, always jump to the last known cursor position.
    " Don't do it when the position is invalid or when inside an event handler
    " (happens when dropping a file on gvim).
    " Also don't do it when the mark is in the first line, that is the default
    " position when opening a file.
    au BufReadPost *
                \ if line("'\"") > 1 && line("'\"") <= line("$")  |
                \   exe "normal! g`\"" |
                \ endif
    " use views to restore more state
    au BufWinLeave * silent! mkview
    au BufWinEnter * silent! loadview
augroup END

" Autoload changes for vimrc
"au BufWritePost .vimrc source ~/.vimrc

set foldtext=CustomFoldText()

" Filetype Specific Configuration {{{1

" Scratch Syntax {{{2
au Filetype scratch setlocal nobackup nowritebackup noswapfile

" Text Syntax {{{2
au FileType text setlocal wrap colorcolumn=

" Help/man Syntax {{{2
au FileType man,Help setlocal nolist colorcolumn=

" Commit Message Syntax {{{2
au FileType svn,gitcommit au! vimmemory
au FileType svn,gitcommit setlocal autoindent

" Vim Syntax {{{2
au FileType vim setlocal textwidth=100
au FileType vim setlocal colorcolumn=+1,+2,+3,+4,+5,+6,+7,+8,+9,+10
au FileType vim setlocal list

" Perl Syntax {{{2
au FileType perl setlocal textwidth=100
au FileType perl setlocal colorcolumn=+1,+2,+3,+4,+5,+6,+7,+8,+9,+10
au FileType perl setlocal equalprg=perltidy
au FileType perl setlocal formatprg=perltidy
au FileType perl setlocal omnifunc=PerlComplete
au FileType perl setlocal list
au FileType perl setlocal keywordprg="perldoc -f"

" Ruby Syntax {{{2
au FileType ruby setlocal textwidth=100
au FileType ruby setlocal colorcolumn=+1,+2,+3,+4,+5,+6,+7,+8,+9,+10
au FileType ruby setlocal omnifunc=rubycomplete#Complete
au FileType ruby let g:rubycomplete_buffer_loading=1
au FileType ruby let g:rubycomplete_classes_in_global=1
au FileType ruby setlocal list
au FileType ruby setlocal keywordprg=ri

" Python Syntax {{{2
au FileType python setlocal textwidth=100
au FileType python setlocal colorcolumn=+1,+2,+3,+4,+5,+6,+7,+8,+9,+10
au FileType python setlocal list
au FileType python setlocal omnifunc=pythoncomplete#Complete
au FileType python setlocal equalprg=pytidy
au FileType python setlocal formatprg=pytidy
au FileType python setlocal tabstop=4
au FileType python setlocal softtabstop=4
au FileType python setlocal shiftwidth=4
au FileType python setlocal smarttab
au FileType python setlocal expandtab
au FileType python setlocal smartindent
au FileType python setlocal keywordprg=pydoc

" Shell Script Syntax {{{2
au FileType sh,bash,zsh setlocal textwidth=100
au FileType sh,bash,zsh setlocal colorcolumn=+1,+2,+3,+4,+5,+6,+7,+8,+9,+10
au FileType sh,bash,zsh setlocal list

" Toggles & Switches (Leader commands) {{{1
let mapleader = ","

" redraw window so search terms are centered
map n nzz
map N Nzz

map <Leader>c :close<cr>

" <space> switches to the next window (give it a second)
" <space>n switches to the next window
" <space><space> switches to the next window and maximizes it
" <space>= Equalizes the size of all windows
" + Increases the size of the current window
" - Decreases the size of the current window
map <space> <c-W>w
map <space>n <c-W>w
map <space><space> <C-W>w<C-W>_
map <space>= <c-W>=
if bufwinnr(1)
    map + <c-W>+
    map - <c-W>-
endif

" Make cursor move ask i'd expect with wrapped lines:
nmap <Down> gj
nmap <Up> gk
nmap j gj
nmap k gk
imap <Down> <Esc>gji
imap <Up> <Esc>gki

" Open the current buffer in a vertically split window with the next page of
" text on it.
noremap <silent> <Leader>vs ggzR:<C-u>let @z=&so<CR>:set so=0 noscb<CR>:set columns=178<CR>:bo vs<CR>zRLjzt:setl scb<CR><C-w>p:setl scb<CR>:let &so=@z<CR>

" Easier navigation between open windows
map <C-k> <C-W>k
map <C-j> <C-W>j
map <C-h> <C-W>h
map <C-l> <C-W>l

" Reselect visually selected text after indenting
vmap > >gv
vmap < <gv

" Select the last edited/pasted text
nmap gv `[v`]
nmap <Leader>= <C-W>=
nmap <Leader>> <C-W>>
nmap <Leader>< <C-W><
" make Y consistant with C and D
noremap Y y$

" Allow tab completion
"imap <Tab> <C-N>
"imap <S-Tab> <C-P>

" Easily modify vimrc
nmap <leader>ev :split $MYVIMRC<CR>
nmap <leader>egv :split $MYGVIMRC<CR>

" Move current line down/up
map <C-Down> ]e
map <C-Up> [e

" Move visually selected lines down/up
vmap <C-Down> ]egv
vmap <C-Up> [egv

" Move visual selection back/forwards
set ww+=<,>
vmap <C-Left> x<Left>P`[v`]
vmap <C-Right> x<Right>P`[v`]

" My main typos, these arent used in commands so why not?
cmap WQ wq
cmap wQ wq

" sudo write-file in case you opened a file you dont have privileges to write
cmap w!! w !sudo tee % >/dev/null<cr>

" Smarter Pasting!
nmap <Leader>P :set paste<CR>"*p;set nopaste<CR>

" Custom commands and functions {{{1
" view diff between current buffer and original file it was loaded from {{{2
nnoremap <Leader>df :call DiffOrig()<CR>

function! DiffOrig()
    if !exists("b:diff_active") && &buftype == "nofile"
        echoerr "E: Cannot diff a scratch buffer"
        return -1
    elseif expand("%") == ""
        echoerr "E: Buffer doesn't exist on disk"
        return -1
    endif

    if !exists("b:diff_active") || b:diff_active == 0
        let b:diff_active = 1
        let l:orig_filetype = &l:filetype

        leftabove vnew
        set buftype=nofile
        read #
        0delete_
        let &l:filetype = l:orig_filetype
        diffthis
        wincmd p
        diffthis
    else
        diffoff
        wincmd p
        bdelete
        let b:diff_active = 0
    endif
endfunction

" Toggles for location and quickfix window {{{2
function! GetBufferList()
    redir =>buflist
    silent! ls
    redir END
    return buflist
endfunction

function! ToggleList(bufname, pfx)
    let buflist = GetBufferList()
    for bufnum in map(filter(split(buflist, '\n'), 'v:val =~ "'.a:bufname.'"'), 'str2nr(matchstr(v:val, "\\d\\+"))')
        if bufwinnr(bufnum) != -1
            exec(a:pfx.'close')
            return
        endif
    endfor
    if a:pfx == 'l' && len(getloclist(0)) == 0
        echohl ErrorMsg
        echo "Location List is Empty."
        return
    endif
    let winnr = winnr()
    exec(a:pfx.'open')
    if winnr() != winnr
        wincmd p
    endif
endfunction

nmap <silent> <leader>ll :call ToggleList("Location List", 'l')<CR>
nmap <silent> <leader>qf :call ToggleList("Quickfix List", 'c')<CR>

" Templates {{{1
autocmd! BufNewFile * silent! 0r ~/.vim/skel/tmpl.%:e

" Diff between buffer and the unwritten file {{{2
if !exists(":DiffOrig")
    command DiffOrig vert new | set bt=nofile | r # | 0d_ | diffthis
                \ | wincmd p | diffthis
endif

" Custom fold text {{{2
fu! CustomFoldText()
    "get first non-blank line
    let fs = v:foldstart
    while getline(fs) =~ '^\s*$' | let fs = nextnonblank(fs + 1)
    endwhile
    if fs > v:foldend
        let line = getline(v:foldstart)
    else
        let line = substitute(getline(fs), '\t', repeat(' ', &tabstop), 'g')
    endif

    let w = winwidth(0) - &foldcolumn - (&number ? 8 : 0)
    let foldSize = 1 + v:foldend - v:foldstart
    let foldSizeStr = " " . foldSize . " lines "
    let foldLevelStr = repeat("+--", v:foldlevel)
    let lineCount = line("$")
    let foldPercentage = printf("[%.1f", (foldSize*1.0)/lineCount*100) . "%] "
    let expansionString = repeat(".", w - strwidth(foldSizeStr.line.foldLevelStr.foldPercentage))
    return line . expansionString . foldSizeStr . foldPercentage . foldLevelStr
endf



" Strip trailing whitespaces  {{{2
function! Preserve(command)
    " Preparation: save last search, and cursor position.
    let _s=@/
    let l = line(".")
    let c = col(".")
    " Do the business:
    execute a:command
    " Clean up: restore previous search history, and cursor position
    let @/=_s
    call cursor(l, c)
endfunction
nmap _$ :call Preserve("%s/\\s\\+$//e")<CR>
nmap _= :call Preserve("normal gg=G")<CR>

" Search for current visual selection with */# {{{2
" Tip tip from: http://amix.dk/blog/viewEntry/19334
function! CmdLine(str)
    exe "menu Foo.Bar :" . a:str
    emenu Foo.Bar
    unmenu Foo
endfunction

function! VisualSearch(direction) range
    let l:saved_reg = @"
    execute "normal! vgvy"

    let l:pattern = escape(@", '\\/.*$^~[]')
    let l:pattern = substitute(l:pattern, "\n$", "", "")

    if a:direction == 'b'
        execute "normal ?" . l:pattern . "^M"
    elseif a:direction == 'gv'
        call CmdLine("vimgrep " . '/'. l:pattern . '/' . ' **/*.')
    elseif a:direction == 'f'
        execute "normal /" . l:pattern . "^M"
    endif

    let @/ = l:pattern
    let @" = l:saved_reg
endfunction

" Generate a color pallette {{{2
function! Colorize()
    let num = 255
    while num >= 0
        exec 'hi col_'.num.' ctermbg='.num.' ctermfg=white'
        exec 'syn match col_'.num.' "ctermbg='.num.':...." containedIn=All'
        call append(0, 'ctermbg='.num.':....')
        let num = num - 1
    endwhile
endfunction

" Pasting Buffers {{{2
fu! PasteWindow(direction)
    if exists("g:yanked_buffer")
        if a:direction == 'edit'
            let temp_buffer = bufnr('%')
        endif

        exec a:direction . " +buffer" . g:yanked_buffer

        if a:direction == 'edit'
            let g:yanked_buffer = temp_buffer
        endif
    endif
endf

"yank/paste buffers
:nmap <silent> <leader>wy  :let g:yanked_buffer=bufnr('%')<cr>
:nmap <silent> <leader>wd  :let g:yanked_buffer=bufnr('%')<cr>:q<cr>
:nmap <silent> <leader>wp :call PasteWindow('edit')<cr>
:nmap <silent> <leader>ws :call PasteWindow('split')<cr>
:nmap <silent> <leader>wv :call PasteWindow('vsplit')<cr>

" Configure plugins {{{1
" Bexec {{{2
nmap <silent> <unique> <F8> :call Bexec()<cr>
vmap <silent> <unique> <F8> :call BexecVisual()<cr>
map <silent> <unique> <F9> :call BexecCloseOut()<cr>


" Screen/tmux {{{2
let g:ScreenImpl = 'Tmux'
"
" Simplenote {{{2
source ~/.simplenoterc
noremap <Leader>sl :Simplenote -l<CR>
noremap <Leader>sn :Simplenote -n<CR>

" Lusty {{{2
noremap <Leader>a :LustyBufferExplorer<CR>
noremap <Leader>f :LustyFilesystemExplorer<CR>
noremap <Leader>r :LustyFilesystemExplorerFromHere<CR>
noremap <Leader>j :LustyJuggler<CR>

" tagbar {{{2
map <leader>t :TagbarToggle<CR>
let g:tagbar_expand = 1
let g:tagbar_sort = 1
let g:tagbar_ctags_use_tagfiles = 1

" Showmarks {{{2
let g:showmarks_enable=1
let g:showmarks_include="abcdefghijklmnopqrstuvwxyz"
let g:showmarks_textlower="\t>"

" bufkill {{{2
map <Leader>k :bd<CR>
map <Leader>k! :bd!<CR>

" Command-T {{{2
map <Leader>ct :CommandT<CR>

" FuzzyFinder {{{2
map <Leader>fb :FufBuffer<CR>
map <Leader>ff :FufFile<CR>

" Ack {{{2
noremap <Leader>g :Ack<space><C-r><C-w><CR>
let ackprg="~/scripts/ack --nocolor --nogroup --column --flush"

" NERDCommenter {{{2
" When we comment something with the NERDComment Toggle, reselect
let g:NERDCreateDefaultMappings = 0
let g:NERDSpaceDelims = 1
vmap <leader>cc <plug>NERDCommenterToggle<cr>vgv
nmap <leader>cc <plug>NERDCommenterToggle
vmap <leader>cb <plug>NERDCommenterSexy<cr>vgv
nmap <leader>cb <plug>NERDCommenterSexy
vmap <leader>c<space> <plug>NERDCommenterToggle<cr>vgv
nmap <leader>c<space> <plug>NERDCommenterToggle
nmap <leader>c$ <plug>NERDCommenterToEOL


" NERDTree.vim {{{2
let NERDTreeIgnore=['\~$', '\.swp$', '^CVS$', '\.svn$', '^CVSROOT$', '^\.git$', '\.pyc$', '\.pyo$', '\.swp$']
map <Leader>dd :NERDTreeToggle<CR>
map <Leader>df :NERDTreeFind<CR>
let NERDTreeShowBookmarks=1
let NERDTreeQuitOnOpen=1
let NERDTreeHighlightCursorLine=1


autocmd WinEnter * call s:CloseIfOnlyNerdTreeLeft()

" Close all open buffers on entering a window if the only
" buffer that's left is the NERDTree buffer
function! s:CloseIfOnlyNerdTreeLeft()
    if exists("t:NERDTreeBufName")
        if bufwinnr(t:NERDTreeBufName) != -1
            if winnr("$") == 1
                q
            endif
        endif
    endif
endfunction


" notes.vim {{{2
let g:notes_location=glob("~/Docs/Notes")

" Scratch {{{2
function! ToggleMyScratch()
    let file_name     = '~/.scratch'
    let buffer_number = bufnr(file_name)
    let window_number = bufwinnr(buffer_number)
    if buffer_number == -1
        exe 'split' . file_name
    else
        if window_number != -1
            if winnr() != window_number
                exe window_number . "wincmd w"
            else
                write
                quit
            endif
        else
            exe 'split' . file_name
        endif
    endif
endfunction
map <leader>s :call ToggleMyScratch()<CR>

" Supertab {{{2
let g:SuperTabDefaultCompletionType = "context"

" Post-local loading (site customizations) {{{1
if filereadable(expand("~/.vimrc.post-local"))
    source ~/.vimrc.post-local
endif

" Modelines: {{{1
" vim: nowrap fdm=marker ft=vim
" }}}
